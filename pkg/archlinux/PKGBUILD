# Maintainer: amtal <github.com/amtal>
pkgname=blindsight-git
pkgver=autofilled
pkgrel=1
pkgdesc="High-density, terminal-based binary viewer for visual pattern matching"
arch=('i686' 'x86_64')
url="https://github.com/amtal/blindsight#readme"
license=('MIT')
groups=()
depends=('tcc' 'ncurses')
makedepends=('autoconf')
optdepends=('libseccomp: sandboxing')
provides=('blindsight')
conflicts=('blindsight')
replaces=('blindsight')
backup=()
source=("blindsight::git+https://github.com/amtal/blindsight.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/blindsight"
  git describe --long | sed 's/^v//;s/-/.r/;s/-/./'
}

build() {
  cd "$srcdir/blindsight"
  ./bootstrap
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/blindsight"
  make DESTDIR="$pkgdir/" install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

